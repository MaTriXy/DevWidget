buildscript {
    ext.ci = 'true' == System.getenv('CI')
    ext.isPlay = project.hasProperty('play')

    def versionMajor = 1
    def versionMinor = 0
    def versionBuild = 0

    ext.versions = [
            compileSdk : 27,
            targetSdk  : 25,
            minSdk     : 21,
            versionCode: versionMajor * 10000 + versionMinor * 1000 + versionBuild,
            versionName: "${versionMajor}.${versionMinor}",

            arch       : '1.1.0',
            butterKnife: '8.8.1',
            dagger     : '2.14.1',
            gms        : '11.8.0',
            kotlin     : '1.2.21',
            room       : '1.1.0-alpha1',
            supportLib : '27.0.2',
    ]

    repositories {
        gradlePluginPortal()
        google()
        jcenter()
        if (isPlay) {
            maven { url 'https://maven.fabric.io/public' }
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.0-alpha02'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
        classpath 'com.github.triplet.gradle:play-publisher:1.2.0'
        classpath 'com.novoda:gradle-android-command-plugin:2.0'
        classpath 'com.novoda:build-properties-plugin:1.2.1'
        classpath 'com.novoda:gradle-static-analysis-plugin:0.5'
        if (isPlay) {
            classpath 'com.google.gms:google-services:3.1.2'
            classpath 'io.fabric.tools:gradle:1.25.1'
        }
    }
}

apply plugin: 'com.github.ben-manes.versions'

allprojects {
    repositories {
        google()
        jcenter()
    }
}
