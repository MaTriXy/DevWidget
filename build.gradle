buildscript {
    ext.ci = 'true' == System.getenv('CI')
    ext.isPlay = project.hasProperty('play')
    ext.configFile = { fileName ->
        rootProject.file("gradle/$fileName")
    }

    def versionMajor = 1
    def versionMinor = 5
    def versionBuild = 7
    ext.playPublishTrack = 'internal'

    ext.versions = [
            compileSdk : 28,
            minSdk     : 21,
            targetSdk  : 28,
            versionCode: versionMajor * 10000 + versionMinor * 1000 + versionBuild,
            versionName: "${versionMajor}.${versionMinor}",

            dagger     : '2.21',
            lifecycle  : '2.0.0',
            kotlin     : '1.3.21',
            okHttp     : '3.11.0',
            room       : '2.1.0-alpha03',
    ]

    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
        jcenter {
            content {
                includeGroup 'org.jetbrains.trove4j'
            }
        }
        if (isPlay) {
            maven { url 'https://maven.fabric.io/public' }
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0-alpha10'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.21.0'
        classpath 'com.novoda:gradle-android-command-plugin:2.0.1'
        classpath 'com.novoda:gradle-build-properties-plugin:0.4.1'
        classpath 'io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.0.0-RC14'
        classpath 'org.jlleitschuh.gradle:ktlint-gradle:7.1.0'
        classpath 'com.novoda:gradle-static-analysis-plugin:0.8.1'
        if (isPlay) {
            classpath 'com.github.triplet.gradle:play-publisher:2.2.0'
            classpath 'com.google.gms:google-services:4.0.2'
            classpath 'io.fabric.tools:gradle:1.27.1'
        }
    }
}

ext.ReporterType = org.jlleitschuh.gradle.ktlint.reporter.ReporterType
apply plugin: 'com.github.ben-manes.versions'

subprojects {
    repositories {
        google()
        mavenCentral()
    }

    if (project.name != 'test-support') {
        apply from: configFile('static-analysis.gradle')
    }
}

ext.ReporterType = org.jlleitschuh.gradle.ktlint.reporter.ReporterType
apply plugin: 'com.github.ben-manes.versions'
apply from: configFile('configure-not-wanted-repositories.gradle')
apply from: configFile('configure-dependency-updates.gradle')
